(*
 * $ cd <JiecLib Project Root>
 * $ jiecc ./stats/descriptive/harmonic_mean.txt -I. -I./vendor/jiecunit/sys -o ./stats/descriptive/harmonic_mean.xml -t omron
 *)
{#ifndef __STATS_DESCRIPTIVE_HARMONIC_MEAN_TXT__}
{#define __STATS_DESCRIPTIVE_HARMONIC_MEAN_TXT__}

{#include <sys.txt>}
{#include <array/array.txt>}

(*{doc 調和平均 n / sum(1/x)。x=0 は不可
@inout data サンプルデータを含む配列。
  正規配列（次元の始点が0で要素数1以上）でなければいけない。
@input n 先頭n個を使用する。
  デフォルト値: -1
@return 計算結果。
@error 入力値が不正な場合。}*)
function Stats_harmonic_mean: lreal
	_PROLOGUE_OF_FUNCTION_()
	var_in_out
		(*{doc サンプルデータを含む配列。}*)
		data: array[*] of lreal;
	end
	var_input
		(*{doc 先頭n個を使用する。}*)
		n: dint := -1;
	end
	var_temp
		actual_n, i: dint;
		acc: lreal;
	end
	{st}
	actual_n := Array_isRegular_lreal(data);
	if (actual_n = 0) or (n > actual_n) then
		eno := false;
		return;
	end_if;
	if n >= 0 then
		actual_n := n;
	end_if;

	acc := 0.0;
	for i := 0 to actual_n - 1 do
		if data[i] = 0.0 then
			eno := false;
			return;
		end_if;
		acc := acc + (1.0 / data[i]);
	end_for;
	Stats_harmonic_mean := dint_to_lreal(actual_n) / acc;
	{end}
end

{#endif}

