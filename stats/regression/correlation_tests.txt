(*
 * $ cd <JiecLib Project Root>
 * $ jiecc ./stats/regression/correlation_tests.txt -I. -I./vendor/jiecunit/sys -o ./stats/regression/correlation_tests.xml -t omron
 *)
{#ifndef __STATS_REGRESSION_CORRELATION_TESTS_TXT__}
{#define __STATS_REGRESSION_CORRELATION_TESTS_TXT__}

{#include <sys.txt>}
{#include <stats/descriptive/correlation.txt>}
{#include <stats/inference/tests/test_core.txt>}

(*{doc Pearson 相関係数の検定（H0: 相関 = 0）
t 統計量 = r * sqrt((n-2) / (1 - r^2))
@inout x サンプルデータを含む配列。
  正規配列（次元の始点が0で要素数1以上）でなければいけない。
@inout y サンプルデータを含む配列。
  正規配列（次元の始点が0で要素数1以上）でなければいけない。
@inout result 結果を格納する。
@error 入力値が不正な場合。}*)
function Stats_test_pearson_correlation
	_PROLOGUE_OF_FUNCTION_()
	var_in_out
		(*{doc x を含む配列。}*)
		x: array[*] of lreal;
		(*{doc y を含む配列。}*)
		y: array[*] of lreal;
	end
	var_in_out
		(*{doc 検定結果。}*)
		result: Stats_TestResult;
	end
	var_temp
		n_x, n_y, n: dint;
		r, t_stat, df: lreal;
	end
	{st}
	n_x := Array_isRegular_lreal(x);
	n_y := Array_isRegular_lreal(y);
	if (n_x = 0) or (n_y = 0) or (n_x <> n_y) then
		eno := false;
		return;
	end_if;
	n := n_x;

	if n < 3 then
		eno := false;
		return;
	end_if;

	r := Stats_pearson(x:=x, y:=y);
	df := dint_to_lreal(n - 2);

	if abs(abs(r) - 1.0) < 1e-10 then
		// 完全相関の場合
		t_stat := 1e10;
	else
		t_stat := r * sqrt(df / (1.0 - r * r));
	end_if;

	result.statistic := r;
	result.pvalue := Stats_p_value_t_two_tailed(t:=t_stat, df:=df);
	result.df := df;
	result.ci_lower := 0.0;
	result.ci_upper := 0.0;
	result.effect_size := r;
	{end}
end

{#endif}
