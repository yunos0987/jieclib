(*
 * $ cd <JiecLib Project Root>
 * $ jiecc ./random/xorshift32.txt -I. -I./vendor/jiecunit/sys -o ./random/xorshift32.xml -t omron
 * or
 * $ jiecc ./random/xorshift32.txt -I. -I./vendor/jiecunit/sys -o ./random/xorshift32.xml -t keyence
 *)
{#ifndef __XORSHIFT32_TXT__}
{#define __XORSHIFT32_TXT__}

{#include <sys.txt>}
{#include <sys_conv.txt>}

(**
 * This file contains the implementation of the Xorshift32 algorithm.
 * Xorshift32 is a simple and fast pseudorandom number generator.
 * It is based on the XOR and bit-shifting operations.
 *)

type
	Xorshift32_t: struct
		state: dword;
	end
end

(**
 * Initializes the Xorshift32 pseudorandom number generator.
 * @param seed The initial seed value for the Xorshift32 algorithm. It must be a 32-bit bits value and non-zero.
 *)
function Xorshift32_init
	_PROLOGUE_OF_FUNCTION_()
	var_in_out self: Xorshift32_t; end
	var_input seed: dword; end
	{st}
	self.state := seed;
	{end}
end

(**
 * Generates a pseudo-random number.
 * @param bits The number of significant bits in the output value; 0 < bits <= 32.
 * @return A pseudo-random number.
 *)
function Xorshift32_next: dword
	_PROLOGUE_OF_FUNCTION_()
	var_in_out self: Xorshift32_t; end
	var_input
		bits: dint := 32;
	end
	var_temp
		x: dword;
	end
	{st}
	x := self.state;
	x := x xor shl(x, 13);
	x := x xor shr(x, 17);
	x := x xor shl(x, 5);
	self.state := x;
	Xorshift32_next := shr(x, 32 - bits);
	{end}
end

(**
 * Generates a uniformly distributed boolean pseudo-random number.
 * @return true or false.
 *)
function Xorshift32_nextBOOL: bool
	_PROLOGUE_OF_FUNCTION_()
	var_in_out self: Xorshift32_t; end
	{st}
	Xorshift32_nextBOOL := Xorshift32_next(self, 1) = dword#1;
	{end}
end

(**
 * Generates a uniformly distributed unsigned 32-bit integer pseudo-random number.
 * @return An unsigned 32-bit integer pseudo-random number, between 0 and 4294967295.
 *)
function Xorshift32_nextUDINT: udint
	_PROLOGUE_OF_FUNCTION_()
	var_in_out self: Xorshift32_t; end
	{st}
	Xorshift32_nextUDINT := dword_to_udint(Xorshift32_next(self, 32));
	{end}
end

{#if not (_KEYENCE)}
(**
 * Generates a uniformly distributed unsigned 64-bit integer pseudo-random number.
 * @return An unsigned 64-bit integer pseudo-random number, between 0 and 18446744073709551615.
 *)
function Xorshift32_nextULINT: ulint
	_PROLOGUE_OF_FUNCTION_()
	var_in_out self: Xorshift32_t; end
	var_temp
		hb, lb: lword;
	end
	{st}
	hb := Xorshift32_next(self, 32);
	lb := Xorshift32_next(self, 32);
	Xorshift32_nextULINT := lword_to_ulint(shl(hb, 32) or lb);
	{end}
end
{#endif}

(**
 * Generates a 32-bit floating-point pseudo-random number with uniform distribution in the range 0.0 to 1.0.
 * @return [0.0, 1.0) of the half-open interval.
 *)
function Xorshift32_nextREAL: real
	_PROLOGUE_OF_FUNCTION_()
	var_in_out self: Xorshift32_t; end
	{st}
	Xorshift32_nextREAL := lreal_to_real(
		udint_to_lreal(dword_to_udint(Xorshift32_next(self, 32))) / lreal#4294967296.0); // 2^32
	{end}
end

(**
 * Generates a 53-bit floating-point pseudo-random number with uniform distribution in the range 0.0 to 1.0.
 * @return [0.0, 1.0) of the half-open interval.
 *)
function Xorshift32_nextLREAL: lreal
	_PROLOGUE_OF_FUNCTION_()
	var_in_out self: Xorshift32_t; end
	var_temp
		av, bv: lreal;
	end
	{st}
	av := udint_to_lreal(dword_to_udint(Xorshift32_next(self, 32 - 5)));
	bv := udint_to_lreal(dword_to_udint(Xorshift32_next(self, 32 - 6)));
	Xorshift32_nextLREAL := (av * lreal#67108864.0 + bv) / lreal#9007199254740992.0;
	{end}
end

{#endif}
