(*
 * $ cd <JiecLib Project Root>
 * $ jiecc ./src/stats/moments/central_moments.txt -I./src -I./vendor/jiecunit/sys -o ./src/stats/moments/central_moments.xml -t omron
 *)
{#ifndef __STATS_MOMENTS_CENTRAL_MOMENTS_TXT__}
{#define __STATS_MOMENTS_CENTRAL_MOMENTS_TXT__}

{#include <sys.txt>}
{#include <array/array.txt>}
{#include <stats/descriptive/mean.txt>}

(*{doc 中心モーメント E((X - E(X))^k)
@inout data サンプルデータを含む配列。
  正規配列（次元の始点が0で要素数1以上）でなければいけない。
@input n 先頭 n 個を使用する。
  デフォルト値: -1
@input k 次数。
@return 計算結果。
@error 入力値が不正な場合。}*)
function Stats_central_moment: lreal
	_PROLOGUE_OF_FUNCTION_()
	var_in_out
		(*{doc サンプルデータを含む配列。}*)
		data: array[*] of lreal;
	end
	var_input
		(*{doc 先頭 n 個を使用する。}*)
		n: dint := -1;
		(*{doc 次数。}*)
		k: dint;
	end
	var_temp
		actual_n, i: dint;
		m, s: lreal;
	end
	{st}
	actual_n := Array_isRegular_lreal(data);
	if (actual_n = 0) or (n > actual_n) then
		eno := false;
		return;
	end_if;
	if n >= 0 then
		actual_n := n;
	end_if;

	if k < 0 then
		eno := false;
		return;
	end_if;

	m := Stats_mean(data:=data, n:=actual_n, eno=>eno);
	if not eno then
		return;
	end_if;

	s := 0.0;
	for i := 0 to actual_n - 1 do
		s := s + (data[i] - m) ** k;
	end_for;
	Stats_central_moment := s / dint_to_lreal(actual_n);
	{end}
end

{#endif}

