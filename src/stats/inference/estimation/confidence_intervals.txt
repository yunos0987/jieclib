(*
 * $ cd <JiecLib Project Root>
 * $ jiecc ./src/stats/inference/estimation/confidence_intervals.txt -I./src -I./vendor/jiecunit/sys -o ./src/stats/inference/estimation/confidence_intervals.xml -t omron
 *)
{#ifndef __STATS_INFERENCE_ESTIMATION_CONFIDENCE_INTERVALS_TXT__}
{#define __STATS_INFERENCE_ESTIMATION_CONFIDENCE_INTERVALS_TXT__}

{#include <sys.txt>}
{#include <stats/descriptive/mean.txt>}
{#include <stats/descriptive/variance.txt>}
{#include <stats/descriptive/stddev.txt>}
{#include <stats/distributions/continuous/student_t.txt>}
{#include <stats/distributions/continuous/chi_square.txt>}

(*{doc t 分布を用いた平均信頼区間。
confidence: 信頼度(0.95 など)。ci_lower, ci_upper: 結果を格納する変数。
@inout data サンプルデータを含む配列。
  正規配列（次元の始点が0で要素数1以上）でなければいけない。
@input n 先頭n個を使用する。
  デフォルト値: -1
@input confidence 信頼度。
@output ci_lower 出力値。
@output ci_upper 出力値。
@error 入力値が不正な場合。}*)
function Stats_ci_mean_t
	_PROLOGUE_OF_FUNCTION_()
	var_in_out
		(*{doc サンプルデータを含む配列。}*)
		data: array[*] of lreal;
	end
	var_input
		(*{doc 先頭n個を使用する。}*)
		n: dint := -1;
		(*{doc 信頼度。}*)
		confidence: lreal;
	end
	var_output
		(*{doc 信頼区間下限値。}*)
		ci_lower: lreal;
		(*{doc 信頼区間上限値。}*)
		ci_upper: lreal;
	end
	var_temp
		actual_n: dint;
		mean_val, std_val, se, t_crit, alpha: lreal;
	end
	{st}
	actual_n := Array_isRegular_lreal(data);
	if (actual_n = 0) or (n > actual_n) then
		eno := false;
		return;
	end_if;
	if n >= 0 then
		actual_n := n;
	end_if;

	if actual_n < 2 then
		eno := false;
		return;
	end_if;

	if confidence <= 0.0 or confidence >= 1.0 then
		eno := false;
		return;
	end_if;

	mean_val := Stats_mean(data:=data, n:=actual_n);
	if not eno then
		return;
	end_if;
	std_val := Stats_sample_stddev(data:=data, n:=actual_n);
	if not eno then
		return;
	end_if;
	se := std_val / sqrt(dint_to_lreal(actual_n));

	alpha := (1.0 - confidence) / 2.0; // t 臨界値の取得（両側）
	t_crit := abs(Stats_student_t_ppf(p:=alpha, df:=dint_to_lreal(actual_n - 1)));

	ci_lower := mean_val - t_crit * se;
	ci_upper := mean_val + t_crit * se;
	{end}
end

(*{doc カイ二乗分布を用いた分散信頼区間。
confidence: 信頼度(0.95 など)。ci_lower, ci_upper: 結果を格納する変数。
@inout data サンプルデータを含む配列。
  正規配列（次元の始点が0で要素数1以上）でなければいけない。
@input n 先頭n個を使用する。
  デフォルト値: -1
@input confidence 信頼度。
@output ci_lower 出力値。
@output ci_upper 出力値。
@error 入力値が不正な場合。}*)
function Stats_ci_variance_chi_square
	_PROLOGUE_OF_FUNCTION_()
	var_in_out
		(*{doc サンプルデータを含む配列。}*)
		data: array[*] of lreal;
	end
	var_input
		(*{doc 先頭n個を使用する。}*)
		n: dint := -1;
		(*{doc 信頼度。}*)
		confidence: lreal;
	end
	var_output
		(*{doc 信頼区間下限値。}*)
		ci_lower: lreal;
		(*{doc 信頼区間上限値。}*)
		ci_upper: lreal;
	end
	var_temp
		actual_n: dint;
		var_val, chi2_lower, chi2_upper, alpha: lreal;
	end
	{st}
	actual_n := Array_isRegular_lreal(data);
	if (actual_n = 0) or (n > actual_n) then
		eno := false;
		return;
	end_if;
	if n >= 0 then
		actual_n := n;
	end_if;

	if (confidence <= 0.0) or (confidence >= 1.0) then
		eno := false;
		return;
	end_if;

	var_val := Stats_sample_variance(data:=data, n:=actual_n, eno=>eno);
	if not eno then
		return;
	end_if;

	alpha := (1.0 - confidence) / 2.0; // カイ二乗の臨界値の取得（両側）
	chi2_lower := Stats_chi_square_ppf(p:=alpha, df:=dint_to_lreal(actual_n - 1), eno=>eno);
	if not eno then
		return;
	end_if;
	chi2_upper := Stats_chi_square_ppf(p:=1.0 - alpha, df:=dint_to_lreal(actual_n - 1), eno=>eno);
	if not eno then
		return;
	end_if;

	if chi2_upper <= 0.0 then
		eno := false;
		return;
	end_if;

	if chi2_lower <= 0.0 then
		eno := false;
		return;
	end_if;

	ci_lower := (dint_to_lreal(actual_n) - 1.0) * var_val / chi2_upper;
	ci_upper := (dint_to_lreal(actual_n) - 1.0) * var_val / chi2_lower;
	{end}
end

(*{doc カイ二乗分布による分散の信頼区間。
Stats_ci_variance_chi_square の別名。
confidence: 信頼度(0.95 など)。ci_lower, ci_upper: 結果を格納する変数。
@inout data サンプルデータを含む配列。
  正規配列（次元の始点が0で要素数1以上）でなければいけない。
@input n 先頭n個を使用する。
  デフォルト値: -1
@input confidence 信頼度。
@output ci_lower 出力値。
@output ci_upper 出力値。
@error 入力値が不正な場合。}*)
function Stats_ci_variance
	_PROLOGUE_OF_FUNCTION_()
	var_in_out
		(*{doc サンプルデータを含む配列。}*)
		data: array[*] of lreal;
	end
	var_input
		(*{doc 先頭n個を使用する。}*)
		n: dint := -1;
		(*{doc 信頼度。}*)
		confidence: lreal;
	end
	var_output
		(*{doc 信頼区間下限値。}*)
		ci_lower: lreal;
		(*{doc 信頼区間上限値。}*)
		ci_upper: lreal;
	end
	{st}
	Stats_ci_variance_chi_square(data:=data, n:=n, confidence:=confidence, ci_lower=>ci_lower, ci_upper=>ci_upper, eno=>eno);
	{end}
end

(*{doc 既知の分散を使った平均信頼区間（正規分布）。sigma: 既知の標準偏差。
confidence: 信頼度(0.95 など)。ci_lower, ci_upper: 結果を格納する変数。
@inout data サンプルデータを含む配列。
  正規配列（次元の始点が0で要素数1以上）でなければいけない。
@input n 先頭 n 個を使用する。
  デフォルト値: -1
@input sigma 既知の標準偏差。
@input confidence 信頼度。
@output ci_lower 出力値。
@output ci_upper 出力値。
@error 入力値が不正な場合。}*)
function Stats_ci_mean_known_var
	_PROLOGUE_OF_FUNCTION_()
	var_in_out
		(*{doc サンプルデータを含む配列。}*)
		data: array[*] of lreal;
	end
	var_input
		(*{doc 先頭 n 個を使用する。}*)
		n: dint := -1;
		(*{doc 既知の標準偏差。}*)
		sigma: lreal;
		(*{doc 信頼度。}*)
		confidence: lreal;
	end
	var_output
		(*{doc 信頼区間下限値。}*)
		ci_lower: lreal;
		(*{doc 信頼区間上限値。}*)
		ci_upper: lreal;
	end
	var_temp
		actual_n: dint;
		mean_val, se, z_crit, alpha: lreal;
	end
	{st}
	actual_n := Array_isRegular_lreal(data);
	if (actual_n = 0) or (n > actual_n) then
		eno := false;
		return;
	end_if;
	if n >= 0 then
		actual_n := n;
	end_if;

	if actual_n < 1 then
		eno := false;
		return;
	end_if;

	if sigma <= 0.0 then
		eno := false;
		return;
	end_if;

	if (confidence <= 0.0) or (confidence >= 1.0) then
		eno := false;
		return;
	end_if;

	mean_val := Stats_mean(data:=data, n:=actual_n, eno=>eno);
	if not eno then
		return;
	end_if;
	se := sigma / sqrt(dint_to_lreal(actual_n));

	alpha := (1.0 - confidence) / 2.0; // z 臨界値の取得（両側）
	z_crit := abs(Stats_normal_ppf(p:=alpha, mu:=0.0, sigma:=1.0, eno=>eno));
	if not eno then
		return;
	end_if;

	ci_lower := mean_val - z_crit * se;
	ci_upper := mean_val + z_crit * se;
	{end}
end

(*{doc 未知の分散を使った平均信頼区間。
Stats_ci_mean_t の別名。
confidence: 信頼度(0.95 など)。ci_lower, ci_upper: 結果を格納する変数。
@inout data サンプルデータを含む配列。
  正規配列（次元の始点が0で要素数1以上）でなければいけない。
@input n 先頭n個を使用する。
  デフォルト値: -1
@input confidence 信頼度。
@output ci_lower 出力値。
@output ci_upper 出力値。
@error 入力値が不正な場合。}*)
function Stats_ci_mean_unknown_var
	_PROLOGUE_OF_FUNCTION_()
	var_in_out
		(*{doc サンプルデータを含む配列。}*)
		data: array[*] of lreal;
	end
	var_input
		(*{doc 先頭n個を使用する。}*)
		n: dint := -1;
		(*{doc 信頼度。}*)
		confidence: lreal;
	end
	var_output
		(*{doc 信頼区間下限値。}*)
		ci_lower: lreal;
		(*{doc 信頼区間上限値。}*)
		ci_upper: lreal;
	end
	{st}
	Stats_ci_mean_t(data:=data, n:=n, confidence:=confidence, ci_lower=>ci_lower, ci_upper=>ci_upper, eno=>eno);
	{end}
end

(*{doc 割合の信頼区間（Wilson score interval）。successes: 成功数
trials: 試行数
confidence: 信頼度(0.95 など)。ci_lower, ci_upper: 結果を格納する変数
@input successes 入力値。
@input trials 入力値。
@input confidence 入力値。
@output ci_lower 出力値。
@output ci_upper 出力値。
@error 入力値が不正な場合。}*)
function Stats_ci_proportion
	_PROLOGUE_OF_FUNCTION_()
	var_input
		(*{doc 成功数。}*)
		successes: dint;
		(*{doc 試行数。}*)
		trials: dint;
		(*{doc 信頼度。}*)
		confidence: lreal;
	end
	var_output
		(*{doc 信頼区間下限値。}*)
		ci_lower: lreal;
		(*{doc 信頼区間上限値。}*)
		ci_upper: lreal;
	end
	var_temp
		prob_hat, n, z, denominator, center: lreal;
	end
	{st}
	if (trials <= 0) or (successes < 0) or (successes > trials) then
		eno := false;
		return;
	end_if;
	if (confidence <= 0.0) or (confidence >= 1.0) then
		eno := false;
		return;
	end_if;

	prob_hat := dint_to_lreal(successes) / dint_to_lreal(trials);
	n := dint_to_lreal(trials);

	// z臨界値の取得（両側）
	z := abs(Stats_normal_ppf(p:=(1.0 - confidence) / 2.0, mu:=0.0, sigma:=1.0, eno=>eno));
	if not eno then
		return;
	end_if;

	// Wilson score interval の計算
	denominator := 1.0 + z * z / n;
	center := (prob_hat + z * z / (2.0 * n)) / denominator;
	ci_lower := center - z * sqrt(prob_hat * (1.0 - prob_hat) / n + z * z / (4.0 * n * n)) / denominator;
	ci_lower := max(ci_lower, 0.0); // note: max(...sqrt(...)..., 0.0) is error on omron.
	ci_upper := center + z * sqrt(prob_hat * (1.0 - prob_hat) / n + z * z / (4.0 * n * n)) / denominator;
	ci_upper := min(ci_upper, 1.0);
	{end}
end

{#endif}

