(*
 * $ cd <JiecLib Project Root>
 * $ jiecc ./src/stats/descriptive/skewness.txt -I./src -I./vendor/jiecunit/sys -o ./src/stats/descriptive/skewness.xml -t omron
 *)
{#ifndef __STATS_DESCRIPTIVE_SKEWNESS_TXT__}
{#define __STATS_DESCRIPTIVE_SKEWNESS_TXT__}

{#include <sys.txt>}
{#include <stats/moments/standardized_moments.txt>}

(*{doc 歪度。Skewness。bias=true: 標本歪度、bias=false: 不偏推定（調整あり）。
@inout data サンプルデータを含む配列。
  正規配列（次元の始点が0で要素数1以上）でなければいけない。
@input n 先頭n個を使用する。
  デフォルト値: -1
@input bias true: 標本歪度, false: 不偏推定
@return 計算結果。
@error 入力値が不正な場合。}*)
function Stats_skewness: lreal
	_PROLOGUE_OF_FUNCTION_()
	var_in_out
		(*{doc サンプルデータを含む配列。}*)
		data: array[*] of lreal;
	end
	var_input
		(*{doc 先頭n個を使用する。}*)
		n: dint := -1;
		(*{doc true: 標本歪度, false: 不偏推定}*)
		bias: bool;
	end
	var_temp
		actual_n: dint;
		skew, adj: lreal;
	end
	{st}
	actual_n := Array_isRegular_lreal(data);
	if (actual_n = 0) or (n > actual_n) then
		eno := false;
		return;
	end_if;
	if n >= 0 then
		actual_n := n;
	end_if;
	if actual_n < 3 then
		eno := false;
		return;
	end_if;

	skew := private_Stats_skewness_from_data(data:=data, n:=actual_n, eno=>eno);
	if not eno then
		return;
	end_if;

	if bias then
		Stats_skewness := skew;
	else
		// 不偏補正: g_1 = sqrt(n*(n-1)) / (n-2) * skew
		adj := sqrt(dint_to_lreal(actual_n * (actual_n - 1))) / dint_to_lreal(actual_n - 2);
		Stats_skewness := skew * adj;
	end_if;
	{end}
end

{#endif}

