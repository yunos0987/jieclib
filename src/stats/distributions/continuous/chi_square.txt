(*
 * $ cd <JiecLib Project Root>
 * $ jiecc ./src/stats/distributions/continuous/chi_square.txt -I./src -I./vendor/jiecunit/sys -o ./src/stats/distributions/continuous/chi_square.xml -t omron
 *)
{#ifndef __STATS_DISTRIBUTIONS_CONTINUOUS_CHI_SQUARE_TXT__}
{#define __STATS_DISTRIBUTIONS_CONTINUOUS_CHI_SQUARE_TXT__}

{#include <sys.txt>}
{#include <stats/distributions/core/special_functions.txt>}

(*{doc カイ二乗分布の確率密度関数。f = 自由度。
f(x) = (x^(df/2-1) * exp(-x/2)) / (2^(df/2) * Gamma(df/2))
対数領域での計算でオーバーフロー回避
ln(f(x)) = (df/2-1)*ln(x) - x/2 - (df/2)*ln(2) - lgamma(df/2)
@input x 入力値。
@input df 自由度。
@return 計算結果。
@error 入力値が不正な場合。}*)
function Stats_chi_square_pdf: lreal
	_PROLOGUE_OF_FUNCTION_()
	var_input
		(*{doc 入力値。}*)
		x: lreal;
		(*{doc 自由度。}*)
		df: lreal;
	end
	var_temp
		power: lreal;
		ln_result: lreal;
	end
	{st}
		Stats_chi_square_pdf := 0.0;
		if (df <= 0.0) or (x < 0.0) then
			eno := false;
			return;
		end_if;
		if x = 0.0 then
			if df <= 2.0 then
				if df = 2.0 then
					Stats_chi_square_pdf := 0.5;
				else
					Stats_chi_square_pdf := 0.0;
				end_if;
			else
				Stats_chi_square_pdf := 0.0;
			end_if;
			return;
		end_if;

		// power = df/2
		power := df / 2.0;

		(* 対数領域での計算
		   ln(f(x)) = (df/2-1)*ln(x) - x/2 - (df/2)*ln(2) - lgamma(df/2) *)
		ln_result := (power - 1.0) * ln(x) - x / 2.0 - power * ln(2.0) - Stats_lgamma(x:=power);
		Stats_chi_square_pdf := exp(ln_result);
	{end}
end

(*{doc カイ二乗分布の累積分布関数
正規化不完全ガンマ関数を使用。CDF = P(df/2, x/2)
P(a,z) = (1/Gamma(a)) * integral_0^z t^(a-1) * exp(-t) dt
@input x 入力値。
@input df 自由度。
@return 計算結果。
@error 入力値が不正な場合。}*)
function Stats_chi_square_cdf: lreal
	_PROLOGUE_OF_FUNCTION_()
	var_input
		(*{doc 入力値。}*)
		x: lreal;
		(*{doc 自由度。}*)
		df: lreal;
	end
	var_temp
		a, z: lreal;
		gln: lreal;   // ln Gamma(a)
		p: lreal;
	end
	{st}
		// domain
		if (df <= 0.0) or (x < 0.0) then
			eno := false;
			Stats_chi_square_cdf := 0.0;
			return;
		end_if;

		if x = 0.0 then
			Stats_chi_square_cdf := 0.0;
			return;
		end_if;

		a := df / 2.0;
		z := x  / 2.0;

		gln := Stats_lgamma(x:=a);  // ln Gamma(a)

		if z < (a + 1.0) then
			// P(a,z) by series
			p := Stats_gammainc(a:=a, z:=z, gln:=gln);
		else
			// P = 1 - Q; Q(a,z) by continued fraction
			p := 1.0 - Stats_gammaincc(a:=a, z:=z, gln:=gln);
		end_if;

		// clamp to [0,1] to absorb roundoff
		if p < 0.0 then p := 0.0; end_if;
		if p > 1.0 then p := 1.0; end_if;
		Stats_chi_square_cdf := p;
	{end}
end

(*{doc カイ二乗分布の分位関数。二分法とNewton-Raphson法を併用
@input p 確率(0..1)。
@input df 自由度。
@return 計算結果。
@error 入力値が不正な場合。}*)
function Stats_chi_square_ppf: lreal
	_PROLOGUE_OF_FUNCTION_()
	var_input
		(*{doc 確率(0..1)。}*)
		p: lreal;
		(*{doc 自由度。}*)
		df: lreal;
	end
	var_temp
		x_low, x_high, x_mid, cdf_low, cdf_high, cdf_mid, cdf_val, pdf_val, delta: lreal;
		i, j: dint;
	end
	{st}
		if df <= 0.0 or p < 0.0 or p > 1.0 then
			eno := false;
			Stats_chi_square_ppf := 0.0;
			return;
		end_if;
		if p = 0.0 then
			Stats_chi_square_ppf := 0.0;
			return;
		end_if;
		if p = 1.0 then
			Stats_chi_square_ppf := df + 10.0 * sqrt(df);
			return;
		end_if;

		// 二分法で根を狭める
		x_low := 0.0;
		x_high := df + 10.0 * sqrt(df);
		cdf_low := Stats_chi_square_cdf(x:=x_low, df:=df);
		cdf_high := Stats_chi_square_cdf(x:=x_high, df:=df);

		// 二分探索。最大 20 回。
		for i := 0 to 19 do
			x_mid := (x_low + x_high) / 2.0;
			cdf_mid := Stats_chi_square_cdf(x:=x_mid, df:=df);

			if cdf_mid < p then
				x_low := x_mid;
				cdf_low := cdf_mid;
			else
				x_high := x_mid;
				cdf_high := cdf_mid;
			end_if;

			if abs(x_high - x_low) < 1e-10 then
				exit;
			end_if;
		end_for;

		// Newton-Raphson で精度向上。最大 5 回。
		x_mid := (x_low + x_high) / 2.0;
		for j := 0 to 4 do
			cdf_val := Stats_chi_square_cdf(x:=x_mid, df:=df);
			pdf_val := Stats_chi_square_pdf(x:=x_mid, df:=df);
			if pdf_val < 1e-15 then
				exit;
			end_if;
			delta := (cdf_val - p) / pdf_val;
			x_mid := x_mid - delta;
			if abs(delta) < 1e-12 then
				exit;
			end_if;
			if x_mid < 0.0 then
				x_mid := 0.0;
			end_if;
		end_for;
		Stats_chi_square_ppf := x_mid;
	{end}
end

(*{doc カイ二乗分布の平均（期待値）。E[X] = df
@input df 自由度。
@return 計算結果。
@error 入力値が不正な場合。}*)
function Stats_chi_square_mean: lreal
	_PROLOGUE_OF_FUNCTION_()
	var_input
		(*{doc 自由度。}*)
		df: lreal;
	end
	{st}
		if df <= 0.0 then
			eno := false;
			Stats_chi_square_mean := 0.0;
			return;
		end_if;
		Stats_chi_square_mean := df;
	{end}
end

(*{doc カイ二乗分布の分散 Var[X] = 2*df
@input df 自由度。
@return 計算結果。
@error 入力値が不正な場合。}*)
function Stats_chi_square_variance: lreal
	_PROLOGUE_OF_FUNCTION_()
	var_input
		(*{doc 自由度。}*)
		df: lreal;
	end
	{st}
		if df <= 0.0 then
			eno := false;
			Stats_chi_square_variance := 0.0;
			return;
		end_if;
		Stats_chi_square_variance := 2.0 * df;
	{end}
end

{#endif}

