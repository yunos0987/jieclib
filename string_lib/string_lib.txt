(*
 * $ cd <JiecLib Project Root>
 * $ jiecc ./string_lib/string_lib.txt -I. -I./vendor/jiecunit/sys -o ./string_lib/string_lib.xml -t omron
 * or
 * $ jiecc ./string_lib/string_lib.txt -I. -I./vendor/jiecunit/sys -o ./string_lib/string_lib.xml -t keyence
 *)
{#ifndef __STRING_LIB_TXT__}
{#define __STRING_LIB_TXT__}

{#include <sys.txt>}

{#define CHAR_MAX_BYTE 4}
{#define STRING string[STRING_MAX_SIZE]}

{doc 文字列の長さを取得する}
function String_length: dint
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
	end
	{st}
	String_length := len(self);
	{end}
end

{doc 指定位置の文字を取得する}
function String_at: string[CHAR_MAX_BYTE + 1]
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
		{doc: 0 origin.}
		p: dint;
	end
	var_temp
		q, l: dint;
	end
	{st}
	l := len(self);
	if p >= 0 then
		q := p;
	else
		q := p + l;
	end_if;
	if (0 <= q) and (q < l) then
		String_at := mid(self, 1, dint_to_uint(q + 1));
	else
		String_at := '';
	end_if;
	{end}
end

{doc 指定位置の文字を取得する}
function String_charAt: string[CHAR_MAX_BYTE + 1]
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
		{doc: 0 origin.}
		p: dint := 0;
	end
	{st}
	if (0 <= p) and (p < uint_to_dint(len(self))) then
		String_charAt := mid(self, 1, dint_to_uint(p + 1));
	else
		String_charAt := '';
	end_if;
	{end}
end

{doc 空文字列か調べる}
function String_empty: bool
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
	end
	{st}
	String_empty := self = ''; // eq('', self), len(self) = 0
	{end}
end

// 文字列を結合する
{#define String_concat(...) _String_concat_n(__VA_ARGC__, __VA_ARGS__)}
	{#define String_concat_1(s1) s1}
	{#define String_concat_2(s1, s2) concat(s1, s2)}
	{#define String_concat_3(s1, s2, s3) concat(String_concat_2(s1, s2), s3)}
	{#define String_concat_4(s1, s2, s3, s4) concat(String_concat_3(s1, s2, s3), s4)}
	{#define String_concat_5(s1, s2, s3, s4, s5) concat(String_concat_4(s1, s2, s3, s4), s5)}
	{#define String_concat_6(s1, s2, s3, s4, s5, s6) concat(String_concat_5(s1, s2, s3, s4, s5), s6)}
	{#define String_concat_7(s1, s2, s3, s4, s5, s6, s7) concat(String_concat_6(s1, s2, s3, s4, s5, s6), s7)}
	{#define String_concat_8(s1, s2, s3, s4, s5, s6, s7, s8) concat(String_concat_7(s1, s2, s3, s4, s5, s6, s7), s8)}
	{#define String_concat_9(s1, s2, s3, s4, s5, s6, s7, s8, s9) concat(String_concat_8(s1, s2, s3, s4, s5, s6, s7, s8), s9)}
	{#define String_concat_10(s1, s2, s3, s4, s5, s6, s7, s8, s9, s10) concat(String_concat_9(s1, s2, s3, s4, s5, s6, s7, s8, s9), s10)}
	{#define String_concat_11(s1, s2, s3, s4, s5, s6, s7, s8, s9, s10, s11) concat(String_concat_10(s1, s2, s3, s4, s5, s6, s7, s8, s9, s10), s11)}
	{#define String_concat_12(s1, s2, s3, s4, s5, s6, s7, s8, s9, s10, s11, s12) concat(String_concat_11(s1, s2, s3, s4, s5, s6, s7, s8, s9, s10, s11), s12)}
	{#define String_concat_13(s1, s2, s3, s4, s5, s6, s7, s8, s9, s10, s11, s12, s13) concat(String_concat_12(s1, s2, s3, s4, s5, s6, s7, s8, s9, s10, s11, s12), s13)}
	{#define String_concat_14(s1, s2, s3, s4, s5, s6, s7, s8, s9, s10, s11, s12, s13, s14) concat(String_concat_13(s1, s2, s3, s4, s5, s6, s7, s8, s9, s10, s11, s12, s13), s14)}
	{#define String_concat_15(s1, s2, s3, s4, s5, s6, s7, s8, s9, s10, s11, s12, s13, s14, s15) concat(String_concat_14(s1, s2, s3, s4, s5, s6, s7, s8, s9, s10, s11, s12, s13, s14), s15)}
	{#define String_concat_16(s1, s2, s3, s4, s5, s6, s7, s8, s9, s10, s11, s12, s13, s14, s15, s16) concat(String_concat_15(s1, s2, s3, s4, s5, s6, s7, s8, s9, s10, s11, s12, s13, s14, s15), s16)}
	{#define _String_concat_n(n, ...) String_concat_@@n(__VA_ARGS__)}

{doc 指定位置のASCII文字コードを取得する}
function String_asciiCharCodeAt: dint
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
		{doc: 0 origin.}
		p: dint := 0;
	end
	var_temp
		c_x: string[CHAR_MAX_BYTE + 1];
		asciicode: dint;
		ASCIIs: string[16#7f + 1] := (*'$00'*)
			'$01$02$03$04$05$06$07$08$09'
			'$0A$0B$0C$0D$0E$0F$10$11$12$13'
			'$14$15$16$17$18$19$1A$1B$1C$1D'
			'$1E$1F !"#$$%&$'()*+,-./0123456789:;<=>?'
			'@ABCDEFGHIJKLMNOPQRSTUVWXYZ[\]^_'
			'`abcdefghijklmnopqrstuvwxyz{|}~$7F' constant;
	end
	{st}
	c_x := String_charAt(self, p);
	if c_x <> '' then
		// note: Poor performance. Should be optimize.
		for asciicode := 1 to 127 by 1 do
			if c_x = String_charAt(ASCIIs, asciicode - 1) then
				String_asciiCharCodeAt := asciicode;
				return;
			end_if;
		end_for;
	end_if;
	String_asciiCharCodeAt := -1;
	{end}
end

{#define String_fromAsciiCharCode(...) _String_fromAsciiCharCode_n(__VA_ARGC__, __VA_ARGS__)}
	{#define String_fromAsciiCharCode_1(c1) String_fromAsciiCharCode1(c1)}
	{#define String_fromAsciiCharCode_2(c1, c2) String_concat(String_fromAsciiCharCode_1(c1), String_fromAsciiCharCode1(c2))}
	{#define String_fromAsciiCharCode_3(c1, c2, c3) String_concat(String_fromAsciiCharCode_2(c1, c2), String_fromAsciiCharCode1(c3))}
	{#define String_fromAsciiCharCode_4(c1, c2, c3, c4) String_concat(String_fromAsciiCharCode_3(c1, c2, c3), String_fromAsciiCharCode1(c4))}
	{#define String_fromAsciiCharCode_5(c1, c2, c3, c4, c5) String_concat(String_fromAsciiCharCode_4(c1, c2, c3, c4), String_fromAsciiCharCode1(c5))}
	{#define String_fromAsciiCharCode_6(c1, c2, c3, c4, c5, c6) String_concat(String_fromAsciiCharCode_5(c1, c2, c3, c4, c5), String_fromAsciiCharCode1(c6))}
	{#define String_fromAsciiCharCode_7(c1, c2, c3, c4, c5, c6, c7) String_concat(String_fromAsciiCharCode_6(c1, c2, c3, c4, c5, c6), String_fromAsciiCharCode1(c7))}
	{#define String_fromAsciiCharCode_8(c1, c2, c3, c4, c5, c6, c7, c8) String_concat(String_fromAsciiCharCode_7(c1, c2, c3, c4, c5, c6, c7), String_fromAsciiCharCode1(c8))}
	{#define String_fromAsciiCharCode_9(c1, c2, c3, c4, c5, c6, c7, c8, c9) String_concat(String_fromAsciiCharCode_8(c1, c2, c3, c4, c5, c6, c7, c8), String_fromAsciiCharCode1(c9))}
	{#define String_fromAsciiCharCode_10(c1, c2, c3, c4, c5, c6, c7, c8, c9, c10) String_concat(String_fromAsciiCharCode_9(c1, c2, c3, c4, c5, c6, c7, c8, c9), String_fromAsciiCharCode1(c10))}
	{#define String_fromAsciiCharCode_11(c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c11) String_concat(String_fromAsciiCharCode_10(c1, c2, c3, c4, c5, c6, c7, c8, c9, c10), String_fromAsciiCharCode1(c11))}
	{#define String_fromAsciiCharCode_12(c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c11, c12) String_concat(String_fromAsciiCharCode_11(c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c11), String_fromAsciiCharCode1(c12))}
	{#define String_fromAsciiCharCode_13(c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c11, c12, c13) String_concat(String_fromAsciiCharCode_12(c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c11, c12), String_fromAsciiCharCode1(c13))}
	{#define String_fromAsciiCharCode_14(c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c11, c12, c13, c14) String_concat(String_fromAsciiCharCode_13(c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c11, c12, c13), String_fromAsciiCharCode1(c14))}
	{#define String_fromAsciiCharCode_15(c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c11, c12, c13, c14, c15) String_concat(String_fromAsciiCharCode_14(c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c11, c12, c13, c14), String_fromAsciiCharCode1(c15))}
	{#define String_fromAsciiCharCode_16(c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c11, c12, c13, c14, c15, c16) String_concat(String_fromAsciiCharCode_15(c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c11, c12, c13, c14, c15), String_fromAsciiCharCode1(c16))}
	{#define _String_fromAsciiCharCode_n(n, ...) String_fromAsciiCharCode_@@n(__VA_ARGS__)}
{doc 指定するASCII文字コードの文字を取得する}
function String_fromAsciiCharCode1: string[CHAR_MAX_BYTE + 1]
	{#define CODE0 16#00}
	{#define CODE1 16#7f}
	_PROLOGUE_OF_FUNCTION_()
	var_input
		charcode: dint;
	end
	var_temp
		CODE2CHAR: array[CODE0..CODE1] of string[1 + 1] := [
			'$00', '$01', '$02', '$03', '$04', '$05', '$06', '$07', '$08', '$09', '$0A', '$0B', '$0C', '$0D', '$0E', '$0F',
			'$10', '$11', '$12', '$13', '$14', '$15', '$16', '$17', '$18', '$19', '$1A', '$1B', '$1C', '$1D', '$1E', '$1F',
			'$20', '$21', '$22', '$23', '$24', '$25', '$26', '$27', '$28', '$29', '$2A', '$2B', '$2C', '$2D', '$2E', '$2F',
			'$30', '$31', '$32', '$33', '$34', '$35', '$36', '$37', '$38', '$39', '$3A', '$3B', '$3C', '$3D', '$3E', '$3F',
			'$40', '$41', '$42', '$43', '$44', '$45', '$46', '$47', '$48', '$49', '$4A', '$4B', '$4C', '$4D', '$4E', '$4F',
			'$50', '$51', '$52', '$53', '$54', '$55', '$56', '$57', '$58', '$59', '$5A', '$5B', '$5C', '$5D', '$5E', '$5F',
			'$60', '$61', '$62', '$63', '$64', '$65', '$66', '$67', '$68', '$69', '$6A', '$6B', '$6C', '$6D', '$6E', '$6F',
			'$70', '$71', '$72', '$73', '$74', '$75', '$76', '$77', '$78', '$79', '$7A', '$7B', '$7C', '$7D', '$7E', '$7F',];
	end
	{st}
	String_fromAsciiCharCode1 := CODE2CHAR[charcode mod (CODE1 + 1)];
	{end}
	{#undef CODE0}
	{#undef CODE1}
end

{doc 小文字へ変換する}
function String_lower: STRING
	{#define CHARCODE_A 16#41}
	{#define CHARCODE_Z 16#5a}
	{#define DIFF_CHARCODE_U2L 16#20}
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
	end
	var_temp
		index, l: dint;
		c_x, d_x: string[CHAR_MAX_BYTE + 1];
		code: dint;
	end
	{st}
	String_lower := '';
	l := len(self);
	for index := 0 to l - 1 by 1 do
		c_x := String_charAt(self, index);
		code := String_asciiCharCodeAt(c_x, 0);
		if (CHARCODE_A <= code) and (code <= CHARCODE_Z) then
			d_x := String_fromAsciiCharCode(code + DIFF_CHARCODE_U2L);
			String_lower := concat(String_lower, d_x);
		else
			String_lower := concat(String_lower, c_x);
		end_if;
	end_for;
	{end}
	{#undef CHARCODE_A}
	{#undef CHARCODE_Z}
	{#undef DIFF_CHARCODE_U2L}
end
{#define String_toLowerCase String_lower}

{doc 大文字へ変換する}
function String_upper: STRING
	{#define CHARCODE_a 16#61}
	{#define CHARCODE_z 16#7a}
	{#define DIFF_CHARCODE_U2L 16#20}
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
	end
	var_temp
		index, l: dint;
		c_x, d_x: string[CHAR_MAX_BYTE + 1];
		code: dint;
	end
	{st}
	String_upper := '';
	l := len(self);
	for index := 0 to l - 1 by 1 do
		c_x := String_charAt(self, index);
		code := String_asciiCharCodeAt(c_x, 0);
		if (CHARCODE_a <= code) and (code <= CHARCODE_z) then
			d_x := String_fromAsciiCharCode(code - DIFF_CHARCODE_U2L);
			String_upper := concat(String_upper, d_x);
		else
			String_upper := concat(String_upper, c_x);
		end_if;
	end_for;
	{end}
	{#undef CHARCODE_a}
	{#undef CHARCODE_z}
	{#undef DIFF_CHARCODE_U2L}
end
{#define String_toUpperCase String_upper}

{doc 文字列同士を比較する}
function String_equals: bool
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
	end
	var_input
		other: STRING;
	end
	{st}
	String_equals := self = other;
	{end}
end

{doc ASCII文字列同士を大文字小文字を無視して比較する}
function String_equalsAsciiIgnoreCase: bool
	{#define CHARCODE_A 16#41}
	{#define CHARCODE_Z 16#5a}
	{#define DIFF_CHARCODE_U2L 16#20}
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
	end
	var_input
		other: STRING;
	end
	var_temp
		index, l: dint;
		self_code, other_code: dint;
	end
	{st}
	l := len(self);
	if l <> len(other) then
		String_equalsAsciiIgnoreCase := false;
		return;
	end_if;

	for index := 0 to l - 1 by 1 do
		self_code := String_asciiCharCodeAt(self, index);
		other_code := String_asciiCharCodeAt(other, index);

		if (self_code < 0) or (other_code < 0) then
			eno := false;
			return;
		end_if;

		if (CHARCODE_A <= self_code) and (self_code <= CHARCODE_Z) then
			self_code := self_code + DIFF_CHARCODE_U2L;
		end_if;
		if (CHARCODE_A <= other_code) and (other_code <= CHARCODE_Z) then
			other_code := other_code + DIFF_CHARCODE_U2L;
		end_if;

		if self_code <> other_code then
			String_equalsAsciiIgnoreCase := false;
			return;
		end_if;
	end_for;

	String_equalsAsciiIgnoreCase := true;
	{end}
	{#undef CHARCODE_A}
	{#undef CHARCODE_Z}
	{#undef DIFF_CHARCODE_U2L}
end

{doc 文字列のFNV-1a(32bit, udint)ハッシュ値を取得する}
function String_hash: udint
	{#define FNV_OFFSET_BASIS udint#2166136261}
	{#define FNV_PRIME udint#16777619}
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
	end
	var_temp
		h: udint;
		index, l: dint;
		code: dint;
	end
	{st}
	h := FNV_OFFSET_BASIS;
	l := len(self);
	for index := 0 to l - 1 by 1 do
		code := String_asciiCharCodeAt(self, index);
		if code < 0 then
			eno := false;
			return;
		end_if;
		h := dword_to_udint(udint_to_dword(h) xor udint_to_dword(dint_to_udint(code)));
		h := h * FNV_PRIME;
	end_for;
	String_hash := h;
	{end}
	{#undef FNV_OFFSET_BASIS}
	{#undef FNV_PRIME}
end

{doc 文字列のFNV-1a(32bit, udint)ハッシュ値をASCII大文字小文字無視で取得する}
function String_hashAsciiIgnoreCase: udint
	{#define FNV_OFFSET_BASIS udint#2166136261}
	{#define FNV_PRIME udint#16777619}
	{#define CHARCODE_A 16#41}
	{#define CHARCODE_Z 16#5a}
	{#define DIFF_CHARCODE_U2L 16#20}
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
	end
	var_temp
		h: udint;
		index, l: dint;
		code: dint;
	end
	{st}
	h := FNV_OFFSET_BASIS;
	l := len(self);
	for index := 0 to l - 1 by 1 do
		code := String_asciiCharCodeAt(self, index);
		if code < 0 then
			eno := false;
			return;
		end_if;
		if (CHARCODE_A <= code) and (code <= CHARCODE_Z) then
			code := code + DIFF_CHARCODE_U2L;
		end_if;
		h := dword_to_udint(udint_to_dword(h) xor udint_to_dword(dint_to_udint(code)));
		h := h * FNV_PRIME;
	end_for;
	String_hashAsciiIgnoreCase := h;
	{end}
	{#undef FNV_OFFSET_BASIS}
	{#undef FNV_PRIME}
	{#undef CHARCODE_A}
	{#undef CHARCODE_Z}
	{#undef DIFF_CHARCODE_U2L}
end

{doc 指定の文字列を削除する}
function String_remove: STRING
	_PROLOGUE_OF_FUNCTION_()
	var_input
		{doc: 入力文字列}
		self: STRING;
		{doc: 削除文字列}
		del: STRING;
		{doc: 最大削除回数}
		count: dint := -1;
	end
	var_temp
		del_len: dint;
		c_x: dint;
		p, l: dint;
		h_str, t_str: STRING;
		r_str: STRING;
	end
	{st}
	del_len := len(del);
	t_str := self;
	l := len(t_str);
	c_x := count;
	while c_x <> 0 do
		p := find(t_str, del);
		if p = 0 then
			exit;
		end_if;
		// e.g. (t_str del)=(abcdef cd) --> p=3 h_str=ab, t_str=ef, l=2
		h_str := mid(t_str, dint_to_uint(p - 1), 1);
		l := l - (p + del_len) + 1;
		if l <> 0 then
			t_str := mid(t_str, dint_to_uint(l), dint_to_uint(p + del_len));
		else
			t_str := '';
		end_if;
		r_str := concat(r_str, h_str);
		c_x := c_x - 1;
	end_while;
	String_remove := concat(r_str, t_str);
	{end}
end

{doc 文字列を逆順に並び替える}
function String_reverse: STRING
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
	end
	var_temp
		p: dint;
	end
	{st}
	String_reverse := '';
	for p := len(self) to 1 by -1 do
		String_reverse := concat(String_reverse, mid(self, 1, dint_to_uint(p)));
	end_for;
	{end}
end

{doc 指定の文字列で始まるか調べる}
function String_startswith: bool
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
		prefix: STRING;
	end
	var_temp
		m: dint;
	end
	{st}
	m := len(prefix);
	if m <> 0 then
		if uint_to_dint(len(self)) >= m then
			String_startswith := prefix = left(self, dint_to_uint(m));
		else
			String_startswith := false;
		end_if;
	else
		String_startswith := true;
	end_if;
	{end}
end

{doc 指定の文字列で終わるか調べる}
function String_endswith: bool
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
		suffix: STRING;
	end
	var_temp
		m: dint;
	end
	{st}
	m := len(suffix);
	if m <> 0 then
		if uint_to_dint(len(self)) >= m then
			String_endswith := suffix = right(self, dint_to_uint(m));
		else
			String_endswith := false;
		end_if;
	else
		String_endswith := true;
	end_if;
	{end}
end

{doc 指定の文字列を含む位置を取得する}
function String_indexOf: dint
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
		searchString: STRING;
		position: dint := 0;
	end
	var_temp
		p, q: dint;
		l: dint;
		str: STRING;
	end
	{st}
	p := max(0, position);
	if searchString <> '' then
		l := len(self);
		if (0 <= p) and (p < l) then
			if p = 0 then
				q := find(self, searchString);
				if q <> 0 then
					String_indexOf := q - 1;
				else
					String_indexOf := -1;
				end_if;
			else
				str := mid(self, dint_to_uint(l - p), dint_to_uint(p + 1));
				q := find(str, searchString);
				if q <> 0 then
					String_indexOf := p + q - 1;
				else
					String_indexOf := -1;
				end_if;
			end_if;
		else
			String_indexOf := -1;
		end_if;
	else
		String_indexOf := p;
	end_if;
	{end}
end

{doc 指定の文字列を後ろから検索する}
function String_lastIndexOf: dint
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
		searchString: STRING;
		position: dint := __DINT_MAX__;
	end
	var_temp
		i: dint;
		l, m: dint;
		start_pos: dint;
		segment: STRING;
	end
	{st}
	// empty needle: 既定は末尾、position指定時はその値
	l := len(self);
	m := len(searchString);
	if m = 0 then
		if position = __DINT_MAX__ then
			String_lastIndexOf := l;
		else
			String_lastIndexOf := position;
		end_if;
		return;
	end_if;
	// 探索開始位置を決定（末尾側から）
	start_pos := min(position, l - m);
	if start_pos < 0 then
		String_lastIndexOf := -1;
		return;
	end_if;
	// 右から左へ走査
	for i := start_pos to 0 by -1 do
		segment := mid(self, dint_to_uint(m), dint_to_uint(i + 1));
		if segment = searchString then
			String_lastIndexOf := i;
			return;
		end_if;
	end_for;
	String_lastIndexOf := -1;
	{end}
end

{doc 指定の文字列を含むか調べる}
function String_includes: bool
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
		searchString: STRING;
		position: dint := 0;
	end
	{st}
	String_includes := String_indexOf(self, searchString, position) <> -1;
	{end}
end

{doc 文字列を指定文字列で分割する}
function String_split: dint
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
		delimiter: STRING := '';
	end
	var_in_out
		outputs: array[*] of STRING;
	end
	var_temp
		rem: STRING;
		p: dint;
		l, m: dint;
		i_0, i_1, index: dint;
	end
	{st}
	i_0 := lower_bound(outputs, 1);
	i_1 := upper_bound(outputs, 1);
	l := len(self);
	m := len(delimiter);
	if m = 0 then
		for p := 1 to l by 1 do
			index := i_0 + p - 1;
			if index > i_1 then
				String_split := i_1 - i_0 + 1;
				return;
			end_if;
			outputs[index] := mid(self, 1, dint_to_uint(p));
		end_for;
		String_split := l;
		return;
	end_if;

	index := i_0;
	rem := self;
	while true do
		if index > i_1 then
			String_split := i_1 - i_0 + 1;
			return;
		end_if;
		p := find(rem, delimiter);
		if p <> 0 then
			if p <> 1 then
				outputs[index] := left(rem, dint_to_uint(p - 1));
			else
				// note: for keyence. error on left(x, 0).
				outputs[index] := '';
			end_if;
			l := l - p - m + 1;
			if l <> 0 then
				rem := mid(rem, dint_to_uint(l), dint_to_uint(p + m));
			else
				// note: for keyence. error on mid(x, 0, p).
				rem := '';
			end_if;
			index := index + 1;
		else
			outputs[index] := rem;
			String_split := index - i_0 + 1;
			return;
		end_if;
	end_while;
	{end}
end

{doc 開始インデクスと終了インデクスを指定して、文字列の部分を取得する}
function String_substring: STRING
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
		indexStart: dint;
		indexEnd: dint := __DINT_MAX__;
	end
	var_temp
		l: dint;
		i_0, i_1: dint;
		temp: dint;
	end
	{st}
	l := len(self);
	i_0 := max(0, indexStart);
	i_0 := min(l, i_0); // memo: omron bug; min(l, max(0, indexStart));
	i_1 := max(0, indexEnd);
	i_1 := min(l, i_1); // memo: omron bug; min(l, max(0, indexEnd))
	if i_0 > i_1 then
		temp := i_0;
		i_0 := i_1;
		i_1 := temp;
	end_if;
	if i_0 <> i_1 then
		String_substring := mid(self, dint_to_uint(i_1 - i_0), dint_to_uint(i_0 + 1));
	else
		String_substring := '';
	end_if;
	{end}
end

{doc 両端の空白を削除する}
function String_trim: STRING
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
	end
	var_temp
		WHITESPACES: array[4] of string[2] := [' ', '$t', '$r', '$n'];
		l, i_0, i_1: dint;
		index, jndex: dint;
		c_x: string[2];
		found: bool;
	end
	{st}
	l := len(self);
	if l = 0 then
		String_trim := '';
		return;
	end_if;
	i_0 := 1;
	i_1 := l;

	for index := 1 to l by 1 do
		c_x := mid(self, 1, dint_to_uint(index));
		found := false;
		for jndex := 0 to 3 by 1 do
			if c_x = WHITESPACES[jndex] then
				i_0 := index + 1;
				found := true;
				exit;
			end_if;
		end_for;
		if not found then
			exit;
		end_if;
	end_for;

	if i_0 > l then
		String_trim := '';
		return;
	end_if;

	for index := l to i_0 by -1 do
		c_x := mid(self, 1, dint_to_uint(index));
		found := false;
		for jndex := 0 to 3 by 1 do
			if c_x = WHITESPACES[jndex] then
				i_1 := index - 1;
				found := true;
				exit;
			end_if;
		end_for;
		if not found then
			exit;
		end_if;
	end_for;

	String_trim := mid(self, dint_to_uint(i_1 - i_0 + 1), dint_to_uint(i_0));
	{end}
end

{doc 左端の空白を削除する}
function String_ltrim: STRING
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
	end
	var_temp
		WHITESPACES: array[4] of string[2] := [' ', '$t', '$r', '$n'];
		l: dint;
		p: dint:= 1;
		index, jndex: dint;
		c_x: string[2];
		found: bool;
	end
	{st}
	l := len(self);
	if l = 0 then
		String_ltrim := '';
		return;
	end_if;

	for index := 1 to l by 1 do
		c_x := mid(self, 1, dint_to_uint(index));
		found := false;
		for jndex := 0 to 3 by 1 do
			if c_x = WHITESPACES[jndex] then
				p := index + 1;
				found := true;
				exit;
			end_if;
		end_for;
		if not found then
			exit;
		end_if;
	end_for;

	if p > l then
		String_ltrim := '';
		return;
	end_if;

	String_ltrim := mid(self, dint_to_uint(l - p + 1), dint_to_uint(p));
	{end}
end
{#define String_trimStart String_ltrim}
{#define String_trimLeft String_ltrim}

{doc 右端の空白を削除する}
function String_rtrim: STRING
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
	end
	var_temp
		WHITESPACES: array[4] of string[2] := [' ', '$t', '$r', '$n'];
		l, p: dint;
		index, jndex: dint;
		c_x: string[2];
		found: bool;
	end
	{st}
	l := len(self);
	if l = 0 then
		String_rtrim := '';
		return;
	end_if;
	p := l;

	for index := l to 1 by -1 do
		c_x := mid(self, 1, dint_to_uint(index));
		found := false;
		for jndex := 0 to 3 by 1 do
			if c_x = WHITESPACES[jndex] then
				p := index - 1;
				found := true;
				exit;
			end_if;
		end_for;
		if not found then
			exit;
		end_if;
	end_for;

	if p < 1 then
		String_rtrim := '';
		return;
	end_if;

	String_rtrim := mid(self, dint_to_uint(p), 1);
	{end}
end
{#define String_trimEnd String_rtrim}
{#define String_trimRight String_rtrim}

{doc 文字列型配列を指定文字列で結合する}
function String_join: STRING
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING := '';
	end
	var_in_out
		a: array[*] of STRING;
	end
	var_temp
		index, i_0, i_1: dint;
	end
	{st}
	i_0 := lower_bound(a, 1);
	i_1 := upper_bound(a, 1);
	String_join := a[i_0];
	for index := i_0 + 1 to i_1 by 1 do
		String_join := concat(String_join, self, a[index]);
	end_for;
	{end}
end

{doc 指定の文字列を置換する}
function String_substitute: STRING
	_PROLOGUE_OF_FUNCTION_()
	var_input
		{doc 入力文字列}
		self: STRING;
		{doc 置換前文字列}
		old: STRING;
		{doc 置換後文字列}
		new: STRING;
		{doc 最大置換回数。-1: 制限なし}
		count: dint := -1;
	end
	var_temp
		old_len: dint;
		c_x: dint;
		p, l: dint;
		h_str, t_str: STRING;
	end
	{st}
	String_substitute := '';
	old_len := len(old);
	t_str := self;
	l := len(t_str);
	c_x := count;
	while c_x <> 0 do
		p := find(t_str, old);
		if p = 0 then
			exit;
		end_if;
		// e.g. abcdef cd p=3 h_str=ab,cd,t_str=ef
		h_str := mid(t_str, dint_to_uint(p - 1), 1);
		l := l - (p + old_len) + 1;
		t_str := mid(t_str, dint_to_uint(l), dint_to_uint(p + old_len));
		String_substitute := concat(String_substitute, h_str, new);
		c_x := c_x - 1;
	end_while;
	String_substitute := concat(String_substitute, t_str);
	{end}
end

{doc 指定の文字列を指定回数繰り返す}
function String_repeat: STRING
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
		{doc: 繰り返し回数（0以上）}
		count: dint := 1;
	end
	var_temp
		c: dint;
	end
	{st}
	String_repeat := '';
	c := count;
	while c > 0 do
		String_repeat := concat(String_repeat, self);
		c := c - 1;
	end_while;
	{end}
end

{doc 左側にパディング文字列を追加する}
function String_padStart: STRING
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
		{doc: 目標の文字列長}
		targetLength: dint := 0;
		{doc: パディング文字列（デフォルト: スペース）}
		padString: STRING := ' ';
	end
	var_temp
		current_len: dint;
		pad_len: dint;
		repeat_count: dint;
		padded: STRING;
	end
	{st}
	current_len := len(self);
	if current_len >= targetLength then
		String_padStart := self;
	else
		if len(padString) = 0 then
			String_padStart := self;
			return;
		end_if;
		pad_len := targetLength - current_len;
		repeat_count := (pad_len + len(padString) - 1) / len(padString);
		padded := String_repeat(padString, repeat_count);
		padded := left(padded, dint_to_uint(pad_len));
		String_padStart := concat(padded, self);
	end_if;
	{end}
end

{doc 右側にパディング文字列を追加する}
function String_padEnd: STRING
	_PROLOGUE_OF_FUNCTION_()
	var_input
		self: STRING;
		{doc: 目標の文字列長}
		targetLength: dint := 0;
		{doc: パディング文字列（デフォルト: スペース）}
		padString: STRING := ' ';
	end
	var_temp
		current_len: dint;
		pad_len: dint;
		repeat_count: dint;
		padded: STRING;
	end
	{st}
	current_len := len(self);
	if current_len >= targetLength then
		String_padEnd := self;
	else
		if len(padString) = 0 then
			String_padEnd := self;
			return;
		end_if;
		pad_len := targetLength - current_len;
		repeat_count := (pad_len + len(padString) - 1) / len(padString);
		padded := String_repeat(padString, repeat_count);
		padded := left(padded, dint_to_uint(pad_len));
		String_padEnd := concat(self, padded);
	end_if;
	{end}
end

{#endif}
